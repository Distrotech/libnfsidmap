#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(libnfsidmap, 0.18, nfsv4-wg@citi.umich.edu)
AC_CONFIG_SRCDIR([nfsidmap.h])
AM_INIT_AUTOMAKE
AM_PROG_LIBTOOL

# Checks for programs.
AC_PROG_CC
AC_PROG_RANLIB

# Checks for libraries.

AC_ARG_ENABLE([ldap],
	[AC_HELP_STRING([--disable-ldap],[Disable support for LDAP @<:@default=detect@:>@])])
if test "x$enable_ldap" != "xno" ; then
	AC_CHECK_HEADER([ldap.h],
		[AC_CHECK_LIB([ldap], [ldap_initialize],
		              [have_ldap="yes"],[have_ldap="no"])],
		[have_ldap="no"])
	if test "x$have_ldap" = "xyes" ; then
		AC_DEFINE([ENABLE_LDAP], 1, [Enable LDAP Support])
		LIBS="$LIBS -lldap"
	elif test "x$enable_ldap$have_ldap" = "xyesno" ; then
		AC_MSG_ERROR(LDAP support not found!)
	fi
fi

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h unistd.h errno.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_UID_T
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_MALLOC
AC_CHECK_FUNCS([strchr strdup])

AC_CONFIG_FILES([Makefile])
AC_OUTPUT(libnfsidmap.pc)
